plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id('io.github.gradle-nexus.publish-plugin') version '1.3.0'
}

version = "0.3.38"

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6

    withJavadocJar()
    withSourcesJar()
}

base {
    group = 'cc.rabbitcontrol'
    archivesName = "rcp"
}


dependencies {

    implementation ('io.kaitai:kaitai-struct-runtime:0.10')
    implementation ('io.netty:netty-all:4.1.106.Final')

    testImplementation ('junit:junit:4.13.2')
}


nexusPublishing {
    repositories {
        sonatype ()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = group + ":" + archivesBaseName
                description = 'RabbitControl is a binary data-format definition to describe data values and ui-elements.'
                url = 'https://rabbitcontrol.cc/'
                licenses {
                    license {
                        name = 'GNU LESSER GENERAL PUBLIC LICENSE Version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
                    }
                }
                developers {
                    developer {
                        name.set('Ingo Randolf')
                        email.set('ingo.randolf@servus.at')
                    }
                }
                scm {
                    url = 'https://github.com/rabbitControl/rcp-java'
                    connection = 'scm:git://github.com/rabbitControl/rcp-java.git'
                    developerConnection = 'scm:git:ssh://github.com:rabbitControl/rcp-java.git'
                }
            }
        }
    }
}

signing {
    sign configurations.archives
    sign publishing.publications.mavenJava
}
